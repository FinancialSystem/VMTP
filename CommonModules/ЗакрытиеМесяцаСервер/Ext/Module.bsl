
#Область ФормированиеДокументовОтражениеНДСПоСтроительствуХозяйственнымСпособом

Процедура Оформление_ФормированиеДокументовОтражениеНДСПоСтроительствуХозяйственнымСпособом(ПараметрыОбработчика) Экспорт
	
	ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр("ru = 'Формирование документов «Отражение НДС по строительству хозяйственным способом»'");
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru='Сформировать'");
	
КонецПроцедуры

Процедура Использование_ФормированиеДокументовОтражениеНДСПоСтроительствуХозяйственнымСпособом(ПараметрыОбработчика) Экспорт
	
	КонецКвартала = (ПараметрыОбработчика.ПараметрыРасчета.КонецПериода = КонецКвартала(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	
	Если НЕ КонецКвартала Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется;
		Возврат;
	КонецЕсли;
	
	Организация = ПараметрыОбработчика.ПараметрыРасчета.Организация;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СебестоимостьТоваров.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ ВТ_ДанныеСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Организация = &Организация
		|	И СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И СебестоимостьТоваров.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
		|	И СебестоимостьТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставОС)
		|	И СебестоимостьТоваров.Стоимость <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НТК_ОтражениеНДСПоСтроительствуХозСпособом.Ссылка КАК Ссылка,
		|	НТК_ОтражениеНДСПоСтроительствуХозСпособом.ОбъектСтроительства КАК ОбъектСтроительства
		|ПОМЕСТИТЬ ВТ_ДанныеДокументов
		|ИЗ
		|	Документ.НТК_ОтражениеНДСПоСтроительствуХозСпособом КАК НТК_ОтражениеНДСПоСтроительствуХозСпособом
		|ГДЕ
		|	НТК_ОтражениеНДСПоСтроительствуХозСпособом.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НТК_ОтражениеНДСПоСтроительствуХозСпособом.Проведен
		|	И НТК_ОтражениеНДСПоСтроительствуХозСпособом.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеСебестоимости.АналитикаРасходов КАК АналитикаРасходов,
		|	ЕСТЬNULL(ВТ_ДанныеДокументов.Ссылка, 0) КАК ОтражениеНДСПоСтроительствуХозСпособом
		|ПОМЕСТИТЬ ВТ_Общая
		|ИЗ
		|	ВТ_ДанныеСебестоимости КАК ВТ_ДанныеСебестоимости
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеДокументов КАК ВТ_ДанныеДокументов
		|		ПО ВТ_ДанныеСебестоимости.АналитикаРасходов = ВТ_ДанныеДокументов.ОбъектСтроительства
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Общая.АналитикаРасходов КАК АналитикаРасходов
		|ИЗ
		|	ВТ_Общая КАК ВТ_Общая
		|ГДЕ
		|	ВТ_Общая.ОтражениеНДСПоСтроительствуХозСпособом = 0";
	
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("НачалоПериода",	НачалоКвартала(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецПериода",	КонецКвартала(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	
	РезультатЗапросаСебестоимость = Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстаткиИОбороты.Регистратор КАК Регистратор,
		|	НДСПредъявленныйОстаткиИОбороты.СуммаБезНДСКонечныйОстаток КАК СуммаБезНДСКонечныйОстаток,
		|	НДСПредъявленныйОстаткиИОбороты.НДСКонечныйОстаток КАК НДСКонечныйОстаток
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Авто, , Организация =  &Организация) КАК НДСПредъявленныйОстаткиИОбороты
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(НДСПредъявленныйОстаткиИОбороты.Регистратор) = ТИП(Документ.НТК_ОтражениеНДСПоСтроительствуХозСпособом)
		|	И НДСПредъявленныйОстаткиИОбороты.СуммаБезНДСКонечныйОстаток < 0
		|	И НДСПредъявленныйОстаткиИОбороты.НДСКонечныйОстаток < 0";
	
	РезультатЗапросаОстатки = Запрос.Выполнить();
	
	Если НЕ РезультатЗапросаСебестоимость.Пустой() ИЛИ НЕ РезультатЗапросаОстатки.Пустой() Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено;
	Иначе	
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеСчетовФактурПоНДСПоСтроительствуХозяйственнымСпособом

Процедура Оформление_ФормированиеСчетовФактурПоНДСПоСтроительствуХозяйственнымСпособом(ПараметрыОбработчика) Экспорт
	
	ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр("ru = 'Формирование счетов-фактур по НДС по строительству хоз. способом'");
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru='Сформировать'");
	
КонецПроцедуры

Процедура Использование_ФормированиеСчетовФактурПоНДСПоСтроительствуХозяйственнымСпособом(ПараметрыОбработчика) Экспорт
	
	КонецКвартала = (ПараметрыОбработчика.ПараметрыРасчета.КонецПериода = КонецКвартала(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	
	Если НЕ КонецКвартала Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется;
		Возврат;
	КонецЕсли;
	
	Организация = ПараметрыОбработчика.ПараметрыРасчета.Организация;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НДСПредъявленныйОстатки.СчетФактура КАК СчетФактура,
		|	НДСПредъявленныйОстатки.СуммаБезНДСОстаток КАК СуммаБезНДСОстаток,
		|	НДСПредъявленныйОстатки.НДСОстаток КАК НДСОстаток,
		|	НДСПредъявленныйОстатки.НДСУпрОстаток КАК НДСУпрОстаток
		|ИЗ
		|	РегистрНакопления.НДСПредъявленный.Остатки(
		|			&Период,
		|			Организация = &Организация
		|				И ТИПЗНАЧЕНИЯ(СчетФактура) = ТИП(Документ.НТК_ОтражениеНДСПоСтроительствуХозСпособом)) КАК НДСПредъявленныйОстатки";
	
	Запрос.УстановитьПараметр("Организация",	Организация);
	Запрос.УстановитьПараметр("Период",			КонецКвартала(ПараметрыОбработчика.ПараметрыРасчета.ПериодРегистрации));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено;
	Иначе	
		ПараметрыОбработчика.ДанныеЭтапа.Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти